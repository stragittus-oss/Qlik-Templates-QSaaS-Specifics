///$tab SUB.GetSysAttributes
/*******************************************************************************************************/


Sub GetSysAttributes (pKeepSysTable)
		
	/*
	
	----------------------------------------------------------------------------------------------------
	    SUBROUTINE: GetSysAttributes
	    VERSION: 1.1.0
	----------------------------------------------------------------------------------------------------
	
	    PURPOSE
	        Detects the current Qlik Sense runtime context (tenant, space, customer, project, environment),
	        derives a logical DataSpace, computes data directories, and exposes all results as global
	        __SYS__* variables for use across the script.
	
	    DESCRIPTION
	        1) Seeds platform/app/engine/tenant context:
	           - __SYS__QlikPlatform='QSaas'
	           - __SYS__AppId=DocumentName(), __SYS__AppName=DocumentTitle()
	           - __SYS__EngineVersion=EngineVersion(), __SYS__StgLdts=Timestamp(Now(), 'YYYY-MM-DD hh:mm:ss')
	           - Reads tenant & space attributes via GetSysAttr(...): tenantId/Name/Url/UrlAlias/Region, spaceId/Name/Type
	        2) Detects personal context when spaceName is empty:
	           - IsPersonalFg=1 → __SYS__SpaceId=-1, __SYS__SpaceName='Personal', __SYS__SpaceType='PERSONAL'
	        3) Parses space naming "Customer.Project.Environment":
	           - __SYS__Customer=SubField(__SYS__SpaceName, '.', 1)
	           - __SYS__Project =SubField(__SYS__SpaceName, '.', 2)
	           - __SYS__Environment defaults to 'PRD' when the 3rd token is empty
	        4) Derives logical data space and folders:
	           - __SYS__DataSpace    = Customer.Project.Data.Environment
	           - __SYS__SpaceDataDir = lib://<DataSpace>:DataFiles
	           - __SYS__AppDataDir   = lib://<DataSpace>:DataFiles/<AppName>
	        5) Materializes all results into temp table __SYS__Attributes and assigns each field to a
	           same-named global variable. Table is dropped by default (see parameter).
	
	    PARAMETER
	        pKeepSysTable
	            0 → drop __SYS__Attributes (default)
	            1 → keep __SYS__Attributes (for debugging/inspection)
	
	    OUTPUT VARIABLES (globals)
	        __SYS__QlikPlatform     → 'QSaas'
	        __SYS__EngineVersion    → EngineVersion()
	        __SYS__TenantId         → GetSysAttr('tenantId')
	        __SYS__TenantName       → GetSysAttr('tenantName')
	        __SYS__TenantUrl        → GetSysAttr('tenantUrl')
	        __SYS__TenantUrlAlias   → GetSysAttr('tenantUrlAlias')
	        __SYS__TenantRegion     → GetSysAttr('tenantRegion')
	        __SYS__SpaceId          → GetSysAttr('spaceId') or -1 if personal
	        __SYS__SpaceName        → e.g. "Customer.Project.PRD" or "Personal"
	        __SYS__SpaceType        → "MANAGED" | "SHARED" | "PERSONAL"
	        __SYS__Customer         → first token of space name
	        __SYS__Project          → second token of space name
	        __SYS__Environment      → third token; defaults to "PRD" if absent
	        __SYS__DataSpace        → "Customer.Project.Data.PRD"
	        __SYS__SpaceDataDir     → lib://Customer.Project.Data.PRD:DataFiles
	        __SYS__AppDataDir       → lib://Customer.Project.Data.PRD:DataFiles/<AppName>
	        __SYS__AppId            → DocumentName()
	        __SYS__AppName          → DocumentTitle()
	        __SYS__StgLdts          → load timestamp
	
	    DEPENDENCIES
	        - GetSysAttr() available (Qlik Cloud / QSE)
	        - Space naming convention "Customer.Project.Environment" for non-personal spaces
	
	    SIDE EFFECTS
	        - Creates (and optionally drops) __SYS__Attributes
	        - Defines/overwrites the globals listed above
	
	    EXAMPLE
	        CALL GetSysAttributes;
	        TRACE Space=$(__SYS__SpaceName) | Env=$(__SYS__Environment) | Data=$(__SYS__DataSpace);
	
	----------------------------------------------------------------------------------------------------
	
	*/
	    
	LET pKeepSysTable =
    	If(Len(pKeepSysTable) = 0,	0,
    	If(pKeepSysTable = 0,		0,
       	If(pKeepSysTable = 1,		1,
  		Null()
        )))
	;

	Unqualify *;
	__SYS__Attributes:
    NoConcatenate
    Load
    	__SYS__QlikPlatform												,
        __SYS__EngineVersion											,
		__SYS__TenantId													,
		__SYS__TenantName												,
		__SYS__TenantUrl												,
		__SYS__TenantUrlAlias											,
		__SYS__TenantRegion												,
		__SYS__SpaceId													,
		__SYS__SpaceName												,
        __SYS__SpaceType												,
        __SYS__SpaceDataDir												,
        __SYS__Customer													,
        __SYS__Project													,
        __SYS__Environment												,
        __SYS__DataSpace												,
    	__SYS__AppId													,
        __SYS__AppName													,
        __SYS__AppDataDir												,
        __SYS__StgLdts													
    ;
    Load 
    	*,
    	'lib://' & __SYS__DataSpace & ':DataFiles'							AS __SYS__SpaceDataDir,
    	'lib://' & __SYS__DataSpace & ':DataFiles/' & __SYS__AppName 		AS __SYS__AppDataDir
    ;
    Load
    	*																,
    	__SYS__Customer			& '.' &
    	__SYS__Project			& '.' &
    	'Data'					& '.' &
    	__SYS__Environment												AS __SYS__DataSpace
    ;
    Load
    	*																,
		If(_subField_3 = '', 'PRD', _subField_3)						AS __SYS__Environment
	;
    Load
    	*																,
		SubField(__SYS__SpaceName, '.', 1)								AS __SYS__Customer,
		SubField(__SYS__SpaceName, '.', 2)								AS __SYS__Project,
        SubField(__SYS__SpaceName, '.', 3)								AS _subField_3
    ;
    Load
    	*																,
        If(IsPersonalFg = 1, -1, _spaceId)								AS __SYS__SpaceId,
        If(IsPersonalFg = 1, 'Personal', _spaceName)					AS __SYS__SpaceName,
		If(IsPersonalFg = 1, 'PERSONAL', Upper(_spaceType))				AS __SYS__SpaceType
    ;
    Load
    	*																,
		If(Len(_spaceName) = 0, 1, 0)									AS IsPersonalFg,
    ;
    Load
    	'QSaas'															AS __SYS__QlikPlatform,
		GetSysAttr('tenantId')											AS __SYS__TenantId,
		GetSysAttr('tenantName')										AS __SYS__TenantName,
		GetSysAttr('tenantUrl')											AS __SYS__TenantUrl,
		GetSysAttr('tenantUrlAlias')									AS __SYS__TenantUrlAlias,
        GetSysAttr('tenantRegion')										AS __SYS__TenantRegion,
		GetSysAttr('spaceId')											AS _spaceId,
		GetSysAttr('spaceName')											AS _spaceName,
		GetSysAttr('spaceType')											AS _spaceType,
    	DocumentName()													AS __SYS__AppId,
        DocumentTitle()													AS __SYS__AppName,
        EngineVersion()													AS __SYS__EngineVersion,
        Timestamp(Now(), 'YYYY-MM-DD hh:mm:ss')							AS __SYS__StgLdts
	AutoGenerate
    	1
	;

	For _i = 1 to NoOfFields('__SYS__Attributes')

    	Let _currField.Name		= FieldName(_i,'__SYS__Attributes');
        Let _currField.Value	= Peek(_currField.Name, 0, __SYS__Attributes);
        Let $(_currField.Name) 	= _currField.Value;
        
    Next _i


	If pKeepSysTable = 0 Then
		Drop Table __SYS__Attributes;
	EndIf
    
	LET _i					= Null();
	LET _currField.Name		= Null();
	LET _currField.Value	= Null();
    LET pKeepSysTable 		= Null();
    
End Sub

/*******************************************************************************************************/